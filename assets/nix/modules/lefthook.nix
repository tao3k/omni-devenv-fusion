{
  inputs,
  __inputs__,
  pkgs,
  config,
  lib,
}:
let
  inherit (inputs.omnibus.inputs.flops.inputs.dmerge) prepend append merge;
  initConfigs =
    (inputs.omnibus.units.configs {
      inputs = {
        inputs = {
          nixpkgs = __inputs__.nixpkgs-latest;
          inherit (inputs.omnibus.flake.inputs) git-hooks;
        };
      };
    }).exports.default;

  lefthook = initConfigs.lefthook;

  # Project scopes shared across conform and cog
  # Maps to: agent/how-to/git-workflow.md
  project-scopes = [
    "nix" # Infrastructure: devenv.nix, modules
    "mcp" # Application: mcp-server logic
    "router" # Logic: Tool routing & intent
    "docs" # Documentation
    "cli" # Tooling: justfile, lefthook
    "deps" # Dependency management
    "ci" # GitHub Actions, DevContainer
    "data" # JSONL examples, assets
    "version" # Version bump commits
    "claude" # Claude configuration
    "git-workflow" # Git workflow documentation
    "mcp-core" # Core MCP server modules
    "inference" # AI inference engine
    "orchestrator" # Orchestrator module
    "git-ops" # Git operations tools
    "agent" # Agent modules: BaseAgent, CoderAgent, ReviewerAgent, skills
    "core" # Core agent modules: BaseAgent, CoderAgent, ReviewerAgent
    "rust-core" # Rust core modules
    "rust-types" # omni-types crate (type unification)
    "rust-io" # omni-io crate (safe file I/O)
    "rust-tokenizer" # omni-tokenizer crate (BPE tokenization)
    "rust-bindings" # Python bindings via PyO3
    "scanner"
  ];
  # Define generator configurations
  generators = [
    {
      name = "lefthook";
      gen =
        (config.omnibus.ops.mkNixago initConfigs.nixago-lefthook)
          initConfigs.lefthook.nix
          initConfigs.lefthook.shell
          initConfigs.lefthook.prettier
          {
            data = {
              pre-commit.commands = {
                prettier = {
                  exclude = append [
                    ".devcontainer.json" # Generated by nix, don't format
                    "CHANGELOG.md"
                    "assets/skills/skill_index.json"
                    # "agent/**"
                  ];
                };
                # Phase 17: Update knowledge XML with repomix
                "update-knowledge-xml" = {
                  glob = "agent/knowledge/**/*.md";
                  run = ''cd agent/knowledge && repomix --config repomix.json --output "$PRJ_ROOT"/.data/project_knowledge.xml '';
                };
                # Add ruff for Python formatting
                "format-python" = {
                  glob = "*.py";
                  run = "uv run ruff format {staged_files}";
                };
                cog-check = {
                  run = "cog check";
                };
                format-rust = {
                  glob = "*.rs";
                  run = "cargo fmt -- {staged_files}";
                };
              };
              commit-msg = lefthook.default.data.commit-msg;
              # Remove unnecessary commands from default pre-commit
              commands = builtins.removeAttrs lefthook.default.data.pre-commit.commands [
                "treefmt" # We use nixfmt instead
                # "hunspell" # Not needed for LLM-generated content
                # "typos" # Not needed for LLM-generated content
              ];
            };
          };
    }
    {
      name = "conform";
      gen =
        (config.omnibus.ops.mkNixago initConfigs.nixago-conform)
          initConfigs.conform.default
          {
            data.commit = {
              conventional = {
                scopes = append project-scopes;
              };
            };
          };
    }
    {
      name = "cog";
      gen =
        (config.omnibus.ops.mkNixago initConfigs.nixago-cog) initConfigs.cog.default
          {
            data = {
              scopes = project-scopes;
              changelog = {
                path = "CHANGELOG.md";
                template = "remote";
                remote = "github.com";
                repository = "omni-dev-fusion";
                owner = "tao3k";
                authors = [
                  {
                    username = "gtrunsec";
                    signature = "Guangtao";
                  }
                ];
              };
            };
          };
    }
  ];

  # Generate all hooks using map
  generatedHooks = map (g: g.gen) generators;
in
{
  config = {
    packages = lib.flatten (map (g: g.__passthru.packages) generatedHooks);
    enterShell = lib.concatMapStringsSep "\n" (g: g.shellHook) generatedHooks;
  };
}
